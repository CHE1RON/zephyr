#!/usr/bin/awk -f

BEGIN { header=1; p=0 }

# header until first blank line
/^$/ {
  header=0
}

/setopt PATH_DIRS/ { next }
/unset _comp_path/ { next }

# skip down to Options
/^# Options$/ {
  p=1
  if ( ENVIRON["REQUIREMENTS_SECTION"] != "" ) {
    print ""
    print "#"
    print ENVIRON["REQUIREMENTS_SECTION"]
    print ""
    print "#"
  }
}

/^# Variables$/ {
  p=0
  if ( ENVIRON["VARIABLES_SECTION"] != "" ) {
    print ENVIRON["VARIABLES_SECTION"]
    print ""
    print "#"
  }
}

/^# Initialization$/ {
  p=1
  gsub(/Initialization/,"Init")
}

/^# Styles$/ {
  p=0
  if ( ENVIRON["MISC_SECTION"] != "" ) {
    print ENVIRON["MISC_SECTION"]
  }
}

# print or don't
!p && !header { next }
{ print }
