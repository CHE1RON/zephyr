#!/bin/zsh

0=${(%):-%N}
ZEPHYR=${0:A:h:h}
ZEPHYR_CACHE=${XDG_CACHE_HOME:=~/.cache}/zephyr
mkdir -p $ZEPHYR_CACHE

ZPREZTODIR=$ZEPHYR_CACHE/plugins/prezto
ZSH_UTILS=$ZEPHYR_CACHE/plugins/zsh-utils
ZSH=$ZEPHYR_CACHE/plugins/ohmyzsh

function sedi {
  sed --version &>/dev/null && sed -i -- "$@" || sed -i "" "$@"
}

function get_repos {
  local -a repos=(
    belak/zsh-utils
    ohmyzsh/ohmyzsh
    sorin-ionescu/prezto
  )
  for repo in $repos; do
    repodir=$ZEPHYR_CACHE/plugins/${repo:t}
    if [[ -d $repodir ]]; then
      echo "Refreshing $repo..."
      git -C $repodir pull --quiet --ff --rebase --autostash
    else
      echo "Cloning $repo..."
      git clone --depth 1 --quiet https://github.com/$repo $repodir
    fi
  done

  gitdir=$ZEPHYR_CACHE/plugins/git
  rm -rf $ZEPHYR_CACHE/plugins/git
  mkdir -p $gitdir
  echo "Getting git completions..."
  curl -so $gitdir/git-completion.bash https://raw.githubusercontent.com/git/git/master/contrib/completion/git-completion.bash
  curl -so $gitdir/git-completion.zsh https://raw.githubusercontent.com/git/git/master/contrib/completion/git-completion.zsh
}

function filter {
  awkfilter=$1; shift
  awk -v PLUGIN_NAME=$PLUGIN_NAME \
      -v PLUGIN_DESC=$PLUGIN_DESC \
      -v UPSTREAM_URL=$UPSTREAM_URL \
      -f $ZEPHYR/bin/filters/$awkfilter
}

function build_from_prezto {
  export PLUGIN_NAME=$1
  export PLUGIN_DESC="${2:-Set $PLUGIN_NAME options and define $PLUGIN_NAME aliases.}"
  export UPSTREAM_URL=https://github.com/sorin-ionescu/prezto/blob/master/${3:-modules/$PLUGIN_NAME/init.zsh}
  prezto_file=$ZPREZTODIR/${3:-modules/$PLUGIN_NAME/init.zsh}
  plugin_file=$ZEPHYR/plugins/$PLUGIN_NAME/$PLUGIN_NAME.plugin.zsh

  plugin_filter=$PLUGIN_NAME
  if [[ ! -f $ZEPHYR/bin/filters/$PLUGIN_NAME ]]; then
    plugin_filter=pass
  fi

  echo "Building $PLUGIN_NAME plugin..."
  cat $prezto_file |
  filter scrub_prezto |
  filter add_header |
  filter $plugin_filter |
  filter add_footer >| $plugin_file
}

function build_from_zsh_utils {
  export PLUGIN_NAME=$1
  export PLUGIN_DESC="${2:-Set $PLUGIN_NAME options and define $PLUGIN_NAME aliases.}"
  export UPSTREAM_URL=https://github.com/belak/zsh-utils/blob/main/${3:-$PLUGIN_NAME/$PLUGIN_NAME.plugin.zsh}
  zshutils_file=$ZSH_UTILS/${3:-$PLUGIN_NAME/$PLUGIN_NAME.plugin.zsh}
  plugin_file=$ZEPHYR/plugins/$PLUGIN_NAME/$PLUGIN_NAME.plugin.zsh

  plugin_filter=$PLUGIN_NAME
  if [[ ! -f $ZEPHYR/bin/filters/$PLUGIN_NAME ]]; then
    plugin_filter=pass
  fi

  echo "Building $PLUGIN_NAME plugin..."
  cat $zshutils_file |
  filter scrub_zshutils |
  filter add_header |
  filter $plugin_filter |
  filter add_footer >| $plugin_file
}

function build_directory {
  build_from_prezto directory
}

function build_terminal {
  build_from_prezto terminal "Sets terminal window and tab titles."
}

function build_editor {
  build_from_prezto editor "Set editor specific key bindings, options, and variables."
}

function build_environment {
  export HEAD_CONTENT=$(
    awk -v SECTION="XDG" -f $ZEPHYR/bin/filters/extract_section $ZEPHYR/plugins/environment/environment.plugin.zsh
  )
  export TAIL_CONTENT=$(
    awk -v SECTION="Misc" -f $ZEPHYR/bin/filters/extract_section $ZEPHYR/plugins/environment/environment.plugin.zsh
  )
  build_from_prezto \
    environment \
    "Set general shell options and define environment variables." \
    runcoms/zprofile

  unset {HEAD,TAIL}_CONTENT
}

function build_history {
  build_from_prezto history
}

function build_zshutils_completion {
  export SECTION_CONTENT=$(
    awk -v SECTION="fpath" -f $ZEPHYR/bin/filters/extract_section $ZEPHYR/plugins/completion/completion.plugin.zsh
  )
  export SECTION_CONTENT2=$(
    awk -v SECTION="Misc" -f $ZEPHYR/bin/filters/extract_section $ZEPHYR/plugins/completion/completion.plugin.zsh
  )
  build_from_zsh_utils completion "Set up zsh completions."
  unset SECTION_CONTENT{,2}

  cp -f $ZSH_UTILS/completion/functions/compstyleinit $ZEPHYR/plugins/completion/functions
  echo "# vim: ft=zsh sw=2 ts=2 et" >> $ZEPHYR/plugins/completion/functions/compstyleinit
  # compstyle_zephyr=$ZEPHYR/plugins/completion/functions/compstyle_zephyr_setup
  # cp -f $ZSH_UTILS/completion/functions/compstyle_zshzoo_setup $compstyle_zephyr
  # sedi -e 's/zsh-utils/zephyr/g' \
  #      -e 's/zshzoo/zephyr/g' \
  #      -e 's|github.com/zephyr|github.com/mattmc3/zephyr|g' \
  #      $compstyle_zephyr
}

function build_compstyle {
  prezto_file=$ZPREZTODIR/${3:-modules/completion/init.zsh}
  zephyr_file=$ZEPHYR/plugins/completion/functions/compstyle_zephyr_setup

  echo "Building $zephyr_file:t..."
  cat $prezto_file |
  filter scrub_prezto |
  filter compstyle >| $zephyr_file
}

function _getupstream {
  get_repos

  # prezto
  build_directory
  build_editor
  build_history
  build_terminal
  build_environment
  build_compstyle

  # zsh-utils
  build_zshutils_completion
}
_getupstream $@
