#!/bin/zsh

##? default-command - run a default command when none is given
function default-command {
  # https://github.com/ohmyzsh/ohmyzsh/tree/master/plugins/magic-enter

  # Only run default commands when in PS1 and command line is empty
  # http://zsh.sourceforge.net/Doc/Release/Zsh-Line-Editor.html#User_002dDefined-Widgets
  if [[ -n "$BUFFER" || "$CONTEXT" != start ]]; then
    return
  fi

  local cmd gitcmd
  zstyle -s ':zephyr:plugin:editor' default-command 'cmd' || cmd="ls"
  zstyle -s ':zephyr:plugin:editor' default-git-command 'gitcmd' || gitcmd="git status -sb"

  if command git rev-parse --is-inside-work-tree &>/dev/null; then
    BUFFER="$gitcmd"
  else
    BUFFER="$cmd"
  fi
}
default-command "$@"
