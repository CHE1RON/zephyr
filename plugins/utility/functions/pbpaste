#!/bin/zsh
function pbpaste {
  # Fallback for missing pbpaste command.
  if (( $+commands[pbpaste] )); then
    command pbpaste
  elif [[ "$OSTYPE" == (cygwin|msys)* ]]; then
    cat /dev/clipboard
  elif (( $+commands[clip.exe] )) && (( $+commands[powershell.exe] )); then
    powershell.exe -noprofile -command Get-Clipboard
  elif [ -n $WAYLAND_DISPLAY ] && (( ${+commands[wl-copy]} )) && (( ${+commands[wl-paste]} )); then
    wl-paste
  elif [ -n $DISPLAY ] && (( ${+commands[xsel]} )); then
    xsel --clipboard --output
  elif [ -n $DISPLAY ] && (( ${+commands[xclip]} )); then
    xclip -selection clipboard -out
  elif (( ${+commands[win32yank]} )); then
    win32yank -o
  elif [[ $OSTYPE == linux-android* ]] && (( $+commands[termux-clipboard-set] )); then
    termux-clipboard-get
  elif [ -n $TMUX ] && (( ${+commands[tmux]} )); then
    tmux save-buffer
  fi
}
pbpaste
