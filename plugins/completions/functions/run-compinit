# function run-compinit {
ZSH_COMPDUMP="${ZSH_COMPDUMP:-${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompdump}"
if ! zstyle -T ':zephyr:plugins:completions' use-xdg-basedirs; then
  ZSH_COMPDUMP=${ZDOTDIR:-$HOME}/.zcompdump
fi
[[ -d "${ZSH_COMPDUMP:h}" ]] || mkdir -p "${ZSH_COMPDUMP:h}"

# allow forcing
if [[ "$1" == '-f' ]] || [[ "$1" == "--force" ]]; then
  shift
  [[ -f "$ZSH_COMPDUMP" ]] && command rm "$ZSH_COMPDUMP"
fi

# load and initialize the completion system ignoring insecure directories with a
# cache time of 20 hours, so it should almost always regenerate the first time a
# shell is opened each day.
# glob magic explained:
#   N - sets null_glob option (no error on 0 results)
#   mh-20 - modified less than 20 hours ago
autoload -Uz compinit
local comp_files=($ZSH_COMPDUMP(Nmh-20))
if (( $#comp_files )); then
  compinit -i -C -d "$ZSH_COMPDUMP"
else
  compinit -i -d "$ZSH_COMPDUMP"
  # keep zcompdump younger than cache time even if it isn't regenerated
  touch "$ZSH_COMPDUMP"
fi

# compile zcompdump, if modified, in background to increase startup speed
{
  if [[ -s "$ZSH_COMPDUMP" && (! -s "${ZSH_COMPDUMP}.zwc" || "$ZSH_COMPDUMP" -nt "${ZSH_COMPDUMP}.zwc") ]]; then
    zcompile "$ZSH_COMPDUMP"
  fi
} &!
# }
