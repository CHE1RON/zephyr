# function run-compinit {
local _zcompdump
if [[ -n "$ZSH_COMPDUMP" ]]; then
  _zcompdump="$ZSH_COMPDUMP"
elif zstyle -t ':zephyr:plugins:completions' use-xdg-basedirs; then
  _zcompdump=${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompdump
else
  _zcompdump=${ZDOTDIR:-$HOME}/.zcompdump
fi
[[ -d "${_zcompdump:h}" ]] || mkdir -p "${_zcompdump:h}"

# allow forcing
if [[ "$1" == '-f' ]] || [[ "$1" == "--force" ]]; then
  shift
  [[ -f "$_zcompdump" ]] && command rm "$_zcompdump"
fi

# load and initialize the completion system ignoring insecure directories with a
# cache time of 20 hours, so it should almost always regenerate the first time a
# shell is opened each day.
# glob magic explained:
#   N - sets null_glob option (no error on 0 results)
#   mh-20 - modified less than 20 hours ago
autoload -Uz compinit
local comp_files=($_zcompdump(Nmh-20))
if (( $#comp_files )); then
  compinit -i -C -d "$_zcompdump"
else
  compinit -i -d "$_zcompdump"
  # keep zcompdump younger than cache time even if it isn't regenerated
  touch "$_zcompdump"
fi

# compile zcompdump, if modified, in background to increase startup speed
{
  if [[ -s "$_zcompdump" && (! -s "${_zcompdump}.zwc" || "$_zcompdump" -nt "${_zcompdump}.zwc") ]]; then
    zcompile "$_zcompdump"
  fi
} &!
# }
